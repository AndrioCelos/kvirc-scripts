alias(KISS::gui)
{
	if($classDefined(KISS::gui)) objects.killclass KISS::gui
	class(KISS::gui,dialog)
	{
		constructor()
		{
			$$->$setCaption("K.I.S.S. 2.0")
			$$->$setMinimumWidth(600)
			$$->$setMinimumHeight(600)
			
			%layout=$new(layout,$$)
			%layout->$setMargin(5)
			%layout->$setResizeMode(Minimum)
			
			%lbSpacer=$new(label,$$)
			%layout->$addMultiCellWidget(%lbSpacer,2,2,0,2)
			%layout->$addMultiCellWidget(%lbSpacer,4,4,0,2)
			%layout->$addMultiCellWidget(%lbSpacer,8,8,0,2)
			%layout->$addWidget(%lbSpacer,9,1)
			
			%lbTitle=$new(label,$$)
			%lbTitle->$setText(<b>K.I.S.S. - The KVIrc Icons Style Setup 2.0</b><br /> \
				$tr("Created by","kiss") HelLViS69 &lt;hellvis69 at altervista dot org&gt;)
			%layout->$addMultiCellWidget(%lbTitle,0,1,0,2)
			
			%lbAsk=$new(label,$$)
			%lbAsk->$setText($tr("What icon set would you like to install?","kiss"))
			%layout->$addMultiCellWidget(%lbAsk,3,3,0,2)
			
			%bgIcons=$new(buttongroup,$$)
			%bgLayout=$new(layout,%bgIcons)
			
			$$->%rbIcons1=$new(radiobutton,%bgIcons)
			$$->%rbIcons1->$setText($tr("Classic Icons","kiss"))
			$$->%rbIcons1->$setToolTip($tr("This set will install the classic old-style KVIrc icons.<br /> \
				Indicated to users with a bit of melancholy :)","kiss"))
			%bgLayout->$addWidget($$->%rbIcons1,0,0)
			objects.connect $$->%rbIcons1 toggled $$ toggle
			
			$$->%rbIcons2=$new(radiobutton,%bgIcons)
			$$->%rbIcons2->$setText($tr("Classic Smiles","kiss"))
			$$->%rbIcons2->$setToolTip($tr("This set will install the brand new icon with the old-style<br /> \
				KVIrc smiles, to make KVIrc modern with a bit of vintage style","kiss"))
			%bgLayout->$addWidget($$->%rbIcons2,1,0)
			objects.connect $$->%rbIcons2 toggled $$ toggle
			
			$$->%rbIcons3=$new(radiobutton,%bgIcons)
			$$->%rbIcons3->$setText($tr("New Icons","kiss"))
			$$->%rbIcons3->$setToolTip($tr("This set will install the brand new icons, shipped with the<br /> \
				KVIrc distribution","kiss"))
			%bgLayout->$addWidget($$->%rbIcons3,2,0)
			objects.connect $$->%rbIcons3 toggled $$ toggle
			%layout->$addWidget(%bgIcons,4,0)
			
			$$->%btnOk=$new(button,$$)
			$$->%btnOk->$setImage(274)
			$$->%btnOk->$setText($tr("Install","kiss"))
			$$->%btnOk->$setEnabled($false)
			%layout->$addWidget($$->%btnOk,9,0)
			objects.connect $$->%btnOk clicked $$ install
			
			%btnCanc=$new(button,$$)
			%btnCanc->$setImage(60)
			%btnCanc->$setText($tr("Exit","kiss"))
			%layout->$addWidget(%btnCanc,9,2)
			objects.connect %btnCanc clicked $$ quit
		}
		
		toggle()
		{
			if(($$->%rbIcons1->$isChecked)||($$->%rbIcons2->$isChecked)||($$->%rbIcons3->$isChecked))
				$$->%btnOk->$setEnabled($true)
			
			if((!$$->%rbIcons1->$isChecked)&&(!$$->%rbIcons2->$isChecked)&&(!$$->%rbIcons3->$isChecked))
				$$->%btnOk->$setEnabled($false)
			
			if($$->%rbIcons1->$isChecked)
				$$->$install(1)
			elseif($$->%rbIcons2->$isChecked)
				$$->$install(2)
			elseif($$->%rbIcons3->$isChecked)
				$$->$install(3)
		}
		
		install($0=id of icon set)
		{
			switch($0)
			{
				case(1)
				{
					%m_szPicsRoot=$file.localdir(pics/KISS/ClassicIcons)
					break
				}
				case(2)
				{
					%m_szPicsRoot=$file.localdir(pics/KISS/ClassicSmiles)
					break
				}
				case(3)
				{
					%m_szPicsRoot=$file.localdir(pics/KISS/NewIcons)
					break
				}
				default
				{
					// Just for sanity check
					debug [KISS]: $tr("You found a bug, please report it","kiss")
				}
			}
			
			%m_szCorePicsRoot=%m_szPicsRoot"/coresmall"
			%files[]=$file.ls(%m_szPicsRoot,f,"*.png")
			if(%files[]#)
			{
				foreach(%file,%files[])
				{
					file.copy -o "%m_szPicsRoot/%file" $file.localdir(pics)"/%file"
				}
			}
			
			%files[]=$file.ls(%m_szCorePicsRoot,f,"*.png")
			if(%files[]#)
			{
				foreach(%file,%files[])
				{
					file.copy -o "%m_szCorePicsRoot/%file" $file.localdir(pics/coresmall)"/%file"
				}
			}
			
			dialog.message("K.I.S.S. 2.0 - Info",$tr("You have to restart KVIrc to make\nchanges effective","kiss"),information,$tr("OK","kiss")){}
		}
		
		quit()
		{
			delete $$
		}
	}
		
	%kiss=$new(KISS::gui)
	%kiss->$show()
}
