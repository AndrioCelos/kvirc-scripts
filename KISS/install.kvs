# K.I.S.S. - The KVIrc Icons Style Setup
# Written by HelLViS69 (2007)

# This addon is pretty simple. It does a single thing and it attempts
# to do it really well. With the comments, the script structure
# should be really straightforward.

# register the script: this must be the first instruction executed
# since it will abort with an error when a greater version is already installed
addon.register("kiss","2.1.0","K.I.S.S. - The KVIrc Icons Style Setup",$tr("Allows to change KVIrc icons sets","kiss"),"3.2.6.20070124",$file.localdir(pics/KISS/kiss_32.png))
{
	# This is our uninstall callback: it will be called by KVIrc when addon.uninstall is invoked
	KISS::uninstall
}

# Ok, addon.register succeeded. We can go on with the installation.

# Get the path that this script was launched from
%mypath = $file.extractPath($0)

# get the installer helper class (this is pretty standard and included in the distro)
parse %mypath/utils/installer.kvs

# The installer will copy our files and generate automatically
# an uninstallation alias for them
%installer = $new(installer,0,myinstaller)

# copy files in each subdirectory
# the pics
%installer->$copyFiles("%mypath/pics/","*.png",$file.localdir("pics/KISS"))
%installer->$copyFiles("%mypath/pics/ClassicIcons/","*.png",$file.localdir("pics/KISS/ClassicIcons"))
%installer->$copyFiles("%mypath/pics/ClassicIcons/coresmall/","*.png",$file.localdir("pics/KISS/ClassicIcons/coresmall"))
%installer->$copyFiles("%mypath/pics/ClassicSmiles/","*.png",$file.localdir("pics/KISS/ClassicSmiles"))
%installer->$copyFiles("%mypath/pics/ClassicSmiles/coresmall/","*.png",$file.localdir("pics/KISS/ClassicSmiles/coresmall"))
%installer->$copyFiles("%mypath/pics/NewIcons/","*.png",$file.localdir("pics/KISS/NewIcons"))
%installer->$copyFiles("%mypath/pics/NewIcons/coresmall/","*.png",$file.localdir("pics/KISS/NewIcons/coresmall"))

# the translations
%installer->$copyFiles("%mypath/locale/","*.mo",$file.localdir("locale"))
# then generate the uninstall alias
%installer->$generateUninstallAlias("KISS::uninstallfiles")
# finally kill the installer helper
delete %installer

# Fetch the sources
include %mypath/src/KISS_gui.kvs
include %mypath/src/KISS_uninstall.kvs

# set the script's configuration callback
# so it can be invoked via GUI from the Addon Management dialog
# and via addon.configure

addon.setconfigurecallback("kiss")
{
	KISS::gui
}

action.create(KISS::gui,"K.I.S.S. - The KVIrc Icons Style Setup",$tr("Allows configuring the \"K.I.S.S.\" addon","kiss"),$file.localdir(pics/KISS/kiss_32.png),$file.localdir(pics/KISS/kiss_16.png))
{
	KISS::gui
}

# create and display our toolbar (we could even avoid that since the user
# can simply drag the actions anywhere he wants), but let's really avoid relying on user's interaction
# and do everything by ourselves.
toolbar.create kiss "K.I.S.S." $file.localdir(pics/KISS/kiss_16.png)
toolbar.additem kiss KISS::gui
toolbar.show kiss

# Done: the addon is installed and running: enjoy :)
# HelLViS69 10.10.2007
