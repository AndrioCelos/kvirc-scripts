class(newsticker::rss::entrylist,list)
{
	// var @%m_uUnreadCount
	
	constructor()
	{
		@%m_uUnreadCount = 0
	}
	
	destructor()
	{
		$this->$clear()
	}
	
	unreadCount()
	{
		return @%m_uUnreadCount;
	}
	
	decreaseUnreadCount()
	{
		@%m_uUnreadCount--
	}
	
	clear()
	{
		@%m_uUnreadCount = 0
		if($this->$moveFirst())
		{
			do {
				delete $this->$current()
			} while($this->$moveNext())
		}
		
		$this->$list::clear()
	}
	
	inSort($0 = newsticker::rss::entry)
	{
		if($0->$isUnread())@%m_uUnreadCount++
		if(!$this->$moveFirst())
		{
			$this->$append($0)
			return
		}
		
		%idx = 0
		do {
			%r = $this->$current();
			if(%r->$date() > $0->$date())
			{
				$this->$insert(%idx,$0)
				return
			}
			%idx++
		} while($this->$moveNext())
		# debug "IN SORT: APPENDING"
		$this->$append($0)
	}
	
	removeBySource($0 = sourcename)
	{
		# debug "REMOVE BY SOURCE "$0
		if(!$this->$moveFirst())return

		%idx = 0
		%toremoveidx = 0
		do {
			if($this->$current()->$source() == $0)
			{
				%toremove[%toremoveidx] = %idx
				%toremoveidx++
				#debug "GOT ONE TO REMOVE"
			}
			%idx++
		} while($this->$moveNext())
		
		%hUnread = $hash();

		if(%toremoveidx == 0)return %hUnread
		
		for(%idx = $(%toremoveidx - 1);%idx >= 0;%idx--)
		{
			#debug "REMOVING THAT ONE"
			%r = $this->$at(%toremove[%idx])
			if(%r != $null)
			{
				%hUnread{%r->$date()}{isUnread} = %r->$isUnread()
				%hUnread{%r->$date()}{isNotified} = %r->$isNotified()
				if(%r->$isUnread())@%m_uUnreadCount--
				#debug "...REALLY REMOVING"
				delete %r
				$this->$remove(%toremove[%idx])
			}
		}
		
		return %hUnread
	}

	firstUnNotified()
	{

		if(!$this->$moveFirst())return $null

		do {
			if(!$this->$current()->$isNotified())
				return $this->$current()
		} while($this->$moveNext())

		return $null
	}

}
