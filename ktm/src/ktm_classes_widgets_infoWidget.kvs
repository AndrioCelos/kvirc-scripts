alias(ktm::classes::widgets::infoWidget)
{
	// classe che si occupa di disegnare la parte centrale del Themes Manager ed implementare gli effetti grafici di transizione tra un tema e l'altro:
	// - anteprima del tema
	// - scheda informativa del tema
	
	class(ktm::infoWidget,widget)
	{
		constructor()
		{
			// inizializzazione dei vari parametri
			@%margin=15
			@%spacing=10
			@%scroll_step=30
			@%actual_x=0
			@%delay=15
			@%opacitystep=0.01
			@%pbar=$new(ktm::httpPBar,$$)
			@%pbar->$move(150,90)
			@%pbar->$setP(0,0)
			@%ind=0
			@$setMouseTracking($true)
		}
		destructor()
		{
		}
	
		// funzione che gestisce il passaggio ad un nuovo tema
		setPix()
		{
			// variabile membro che contiene il tema corrent
			@%actual=$0
			// se il tema ha già un'anteprima, nascondiamo la progress bar
			if (!@%actual->%nopreview) @%pbar->$hide()
		}
		
		// settiamo una dimensione fissa della widget
		showEvent()
		{
			@$setMinimumWidth($(300+(@%margin*2)))
			@$setMinimumHeight($(200+(@%margin*2)))
		}
	
		// il timerevent viene usato per gestire e variare i parametri necessari all'effetto "fade" dei temi:
		// il nuovo tema entra scorrendo e con un effetto di dissolvenza rispetto al tema precedente
		timerEvent()
		{
			@%opacity+=@%opacitystep
			if (@%opacity>1.01) @%opacity=1
			if (@%transition=="L")
			{
				@%x-=@%scroll_step
				if(@%x<=0) %stop=$true
			}
			else
			{
				@%x+=@%scroll_step
				if(@%x>=0) %stop=$true
			}
			if (%stop)
			{
				@%transition=$false
				@$killTimer(@%timer_id)	
				@%actual=@%nextpix
				@%x=0
				@%timer_id=
				@%actual_x=0
				if(@%actual->%nopreview) @%pbar->$show
				else @%pbar->$hide
			}
			// forziamo un repaint della widget
			@$repaint()
		}
		
		// funzione che gestisce la progressbar in base al tema corrent
		setPB()
		{
			if($2==@%actual->%id)
			{
				@%actual->%prog=$0
				@%actual->%total=$1
				@%pbar->$setP($0,$1)
			}
			else @%pbar->$setP(0,0)
		}
	
		// slot che viene chiamato quando è stata scaricata completamente la preview del tema corrente
		done()
		{
			if($0==@%actual->%id) 
			{
				@%actual->%nopreview=$false
				@%pbar->$hide()
				%x=@$globalcursorx()
				%y=@$globalcursory()
				%xy[]=@$mapFromglobal(%x,%y)
				@$mouseMoveEvent(0,%xy[0],%xy[1])
				@$update()
			}
		}
	
		// funzione che viene chiamata quando lil tema corrente sta cambiando (l'utente ha premuto il pulsante "Prossimo" o "Precedente")
		// parametri in ingresso: nuovo tema, transizione ("L" ingresso tema verso sinistra - "R" verso destra)
		itemChanged()
		{
			@%nextpix=$0
			if (@%timer_id) @$killTimer(@%timer_id)
			@%opacity=0;
			@%transition=$1;
			if (@%transition=="L") @%x=@$width()
			else @%x=$(-@$width())
			@%actual_x=0
			if (!@%nextpix->%prog && !@%nextpix->%total) @%pbar->$setP(0,0)
			else @%pbar->$setP(@%nextpix->%prog,@%nextpix->%id)
			@%pbar->$hide
			@%timer_id=@$startTimer(@%delay);
		}
	
		// questo evento gestisce il flag che ci fa capire se il mouse sta dentro il perimetro della preview: 
		// nel qual caso viene attivata la possibilità di cliccarci sopra per vedere l'anteprima a risoluzione maggiore.
		mouseMoveEvent()
		{
			if (@%actual->%nopreview) return
			%x=$1;
			%y=$2;
			if (%x>=$(@%actual_x+@%margin) && %x<$(@%actual_x+@%margin+@%actual->%pixmap->$width()))
			{
				if (%y>=@%margin && %y<@%actual->%pixmap->$height() && !@%over) @%over=$true()	
			}
			else @%over=$false()
			@$update()
		}
	
		// questo evento inizializza e visualizza l'anteprima (se la stessa esiste) e il puntatore del mouse sta dentro il perimetro della preview	
		mouseReleaseEvent()
		{
			if (@%over && !$objects.exists(@%preview))
			{
				@%preview=$new(ktm::previewWidget)
				@%preview->$setPixmap(@%actual->%shot)
				@%over=$false()
				@$update()
				@%preview->$show()
			}
		}
	
		// parte principale che disegna la preview e le info
		paintEvent()
		{
			@%p=$0
			//echo paintevent info
			@%p->$setPen(0,0,0)
			@%p->$setAntiAliasing($true)
			@%p->$setTextAntiAliasing($true)
			@%p->$setPenWidth(1)
			@%p->$setBrush(244,151,6,rgb,130)
			//@%p->$setBrushStyle("Dense4Pattern")
			@%p->$drawRoundRect(5,5,0,0,$(@$width()-0),$(@$height()-1))
		//	@%p->$drawRoundRect(5,5,0,0,$(@$width()-0+4),$(@$height()-1+4))
			if (@%transition)
			{
				@%p->$setOpacity($(1-$(@%opacity*2)))
				if (@%transition=="L")@%actual_x-=@%scroll_step
				else  @%actual_x+=@%scroll_step
			}
			@%p->$drawPixmap($(@%actual_x+@%margin),@%margin,@%actual->%pixmap,0,0,@%actual->%pixmap->$width(),@%actual->%pixmap->$height())
			@$drawThemeInfo($(@%actual->%pixmap->$width()+@%spacing+@%margin+@%actual_x),@%margin,@%actual,$(@$width-@%actual->%pixmap->$width()-@%margin-@%spacing),@$height())
			if (@%transition)
			{
				@%p->$translate(@%x,0)
				@%p->$setOpacity(@%opacity)
				@%p->$drawPixmap(@%margin,@%margin,@%nextpix->%pixmap,0,0,@%nextpix->%pixmap->$width(),@%nextpix->%pixmap->$height())
				@$drawThemeInfo($(@%nextpix->%pixmap->$width()+@%spacing+@%margin),@%margin,@%nextpix,$(@$width-@%nextpix->%pixmap->$width()-@%margin-@%spacing),@$height())
		
			}
			else{
				if (@%over)
				{
					@%p->$setFontSize(5)
					%metric=@%p->$fontMetricsWidth($tr("Click thumb to preview"))
					%asc=@%p->$fontAscent()
					@%p->$setBrush(255,255,255,rgb,170)
					@%p->$setPenWidth(1)
					@%p->$drawEllipse($(@%actual_x+@%margin+4),$(@%margin+4),$(%metric+15),$(%asc*3))
					@%p->$drawHtmlText($(@%actual_x+@%margin+5+4),$(@%margin+4+4),$tr("Click thumb to preview"),@$width(),@$height())
				}
			}
		}
		
		// funzione che recupera le info del tema  formattandole come testo html e disegnandole su schermo.
		drawThemeInfo("x,y,theme_object,rect_width,rect_height")
		{
			%x=$0
			%y=$1
			%theme=$2
			%w=$3
			%h=$4
			%text .="<b>Title:</b> "%theme->%title"<br>"
			%text .="<b>Version:</b> "%theme->%version"<br>"
			%text .="<b>Author:</b> "%theme->%author"<br>"
			%text .="<b>Description:</b> "%theme->%description"<br>"
			@%p->$drawHtmlText(%x,%y,%text,%w,%h)
	
		}
	
		// facciamo in modo di avere una dimensione fissa per la widget
		sizeHintRequestEvent()
		{
			return $array($(300+100),200)
		}
	}
}

