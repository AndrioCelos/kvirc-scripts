class("NGLfserv::classes::customwidgets::dirview","listview")
{
	function addTo
	{
dialog.file(dir,%NGLlogo,,,$$,$0)
{
	if (!$0) $1->$returnFromDialog($false)
	else{
		%path=$0
		$1->$fileDroppedEvent($2,%path)
	}
}
	}
	function check
	{
if ($0==$$) return $true
else return $0->$isChecked()
	}
	function clear
	{
%children[]=$$->$children()
for (%i=0;%i<$length(%children[]);%i++) delete %children[%i]
	}
	function constructor
	{
$$->%pix_folder=$new(pixmap)
$$->%pix_folder->$load(%G_icons->$getIcon(folder))
$$->%pix_folder_open=$new(pixmap)
$$->%pix_folder_open->$load(%G_icons->$getIcon(folderopen))
$$->%pix_redfolder=$new(pixmap)
$$->%pix_redfolder->$load(%G_icons->$getIcon(redfolder))
$$->%pix_redfolder_open=$new(pixmap)
$$->%pix_redfolder_open->$load(%G_icons->$getIcon(redfolderopen))
$$->%pix_image_file=$new(pixmap)
$$->%pix_image_file->$load(%G_icons->$getIcon(image))
$$->%pix_audio_file=$new(pixmap)
$$->%pix_audio_file->$load(%G_icons->$getIcon(sound))
$$->%pix_video_file=$new(pixmap)
$$->%pix_video_file->$load(%G_icons->$getIcon(video))
$$->%pix_default_file=$new(pixmap)
$$->%pix_default_file->$load(%G_icons->$getIcon(default))
$$->$addColumn($tr("Shared"),-1)
$$->$addColumn($tr("Size or Dir"),-1)
$$->$setRootIsDecorated(1)
$$->$setSelectionMode("Single")
@$setSortingEnabled($false)
//$$->$setSorting(0,$false)
	}
	function createTree
	{
if ($$->%rootnode) $$->$deleteNode($$)
$$->%rootnode=$0
$$->%virtual_node=$$->%rootnode->$firstNode()
objects.connect $$->%rootnode updated $$ refreshView
if ($$->%rootnode->%noNode) {
	NGLfserv::corefunctions::ngldebug empty TREE 
	$$->%nodata=$true
	return $true
}
	}
	function currentChangedEvent
	{
if ($objects.exists($0)){
	 $$->%item_clicked=$0
	 $$->$emit(currentChanged,$0)
}
else $$->%item_clicked=$$
	}
	function deleteChildren
	{
%children[]=$0->$children()
	for (%i=0;%i<$length(%children[]);%i++) delete %children[%i]
	}
	function deleteNode
	{
%node=$0
%children[]=%node->$children()
%len=$length(%children[])
for (%i=0;%i<%len;%i++) delete %children[%i]
	}
	function fileDroppedEvent
	{
if (!$objects.exists($0)) %node=$$
else %node=$0
%path=$str.replace($1,"\/","\\")
if (%node->%virtual_node) %parent=%node
else %parent=%node->$parent()
if ($file.type($1)!="d") NGLfserv::corefunctions::nglwarning "You can add only folders!"
else{
	if (!%parent->%virtual_node) %node=$$->%rootnode->$firstNode()
	else %node=%parent->%virtual_node
	%check=$$->%rootnode->$addFolder(%path,%node)
	NGLfserv::corefunctions::ngldebug check %check
	 if ($typeof(%check)=="hobject"){
		$$->%rootnode->$setChanged($true)
		%rootpath = $str.replace(%path,"/","\\")
		// fixato con la / 
		if($str.right(%rootpath, 1) != "/") %roothpath .= "/"
		$$->%rootnode->$appendNotYetAcceptedExtradir(%rootpath,$$->%rootnode->$getLocalTriggerPath(%parent->%virtual_node),%check)
	}
	else
		if (!%check)  NGLfserv::corefunctions::nglwarning "Folder already present!"				
}
	}
	function getNode
	{
if ($0->%virtual_node==$1) return $0
%nodes[]=$0->$children()
%len=$length(%nodes[])
for (%i=0;%i<%len;%i++){
	if ($$->$getNode(%nodes[%i],$1)){
		%fl=$true
		break
	}
}
if (%fl) return %nodes[%i]
	}
	function getParentNode
	{
if ($$->%item_clicked->%virtual_node) return $$->%item_clicked
else return $$->%item_clicked->$parent()
	}
	function itemClickedEvent
	{
if ($objects.exists($0)){
 		if ($0->%virtual_node){
 		  	$$->$setChildrenChecked($0,$0->$isChecked)
 		  	if (!$0->$isChecked){ 
 		  		%i=$0
 		  		while(%i!=$$){ 
 		  			%i->$setChecked($false)
 		  			$$->%rootnode->$setDirNotChecked(%i->%virtual_node)
 		  		 	if (!$$->%rootnode->$checkChildrenNotChecked(%i->%virtual_node->$parent())){
 		  		 		$$->%rootnode->$setFoldersNotChecked(%i->%virtual_node)
 		  		 	 	break
 		  			}
 		  			else{
 		  			 	%i=%i->$parent()
 		  			}
 		  		 }
 		  	}
 		  	else { 
 		  		$$->%rootnode->$setDirChecked($0->%virtual_node)
 		  		$$->%rootnode->$setFoldersChecked($0->%virtual_node)
				$$->$setParentChecked($0)
			}
 		  }
 		else{
 			%node=$0->$parent()->%virtual_node
 			if (!$0->$isChecked){ 
 				$$->%rootnode->$setFileNotChecked(%node,$0->$text(0))
 				%i=$0->$parent()
 				while (%i !=$$){
 					 if ($$->%rootnode->$checkChildrenNotChecked(%i->%virtual_node)){
 					 	%i->$setChecked($false)
 					 	$$->%rootnode->$setDirChecked(%i->%virtual_node)
 		  			 	%i=%i->$parent()
 		  			 }
 		  		 	else {
 		    		 		break
 		  		 	}
 		  		 }
 		  	}
 		  	else { 
 		  		$$->%rootnode->$setFileChecked(%node,$0->$text(0))
 		  		if ($0->$parent()!=$$){
 		  			%i=$0->$parent()
 		  			%i->$setChecked($true)
 		  			$$->%rootnode->$setDirNotChecked(%i->%virtual_node)
 		  			$$->$setParentChecked(%i)
 		  		}		  		 
 		  	}
 		  }
 	}
	}
	internal function itemCollapsedEvent
	{
$$->$deleteNode($0)
$new(listviewitem,$0)
if ($$->%rootnode->$isItemHighlighted($0->%virtual_node))
	$0->$setPixmap(0,$$->%pix_redfolder)
else
	$0->$setPixmap(0,$$->%pix_folder)
	}
	internal function itemExpandedEvent
	{
delete $0->$firstChild()
if (!$objects.exists($0->%virtual_node)) return
if ($$->%rootnode->$isItemHighlighted($0->%virtual_node))
	$0->$setPixmap(0,$$->%pix_redfolder_open)
else
	$0->$setPixmap(0,$$->%pix_folder_open)
$$->$showNode($0)
	}
	function output
	{
if ($objects.exists($$->%info_widget)) $$->%info_widget->$append($0)
else echo $0
	}
	function popuphandler
	{
$$->$addTo($$->%item_clicked)
 /*
switch ($0)
{
	case ($$->%id_add)
	{
		 $$->$addTo($$->%item_clicked)
		 break
	}

}
*/
	}
	function refreshView
	{
if ($0==$$->%rootnode->$firstNode() || $0==$$->%rootnode){
		$$->$deleteChildren($$)
		$$->%virtual_node=$$->%rootnode->$firstNode()
		$$->$showNode($$)
	}	 		
 	else{
 		%listviewnode=$$->$getNode($$,$0)
 		if (!%listviewnode) return
 		$$->$deleteNode(%listviewnode)
 		$$->$itemExpandedEvent(%listviewnode)
 	}
	}
	function remove
	{
if ($$->%item_clicked->%virtual_node){
	if($dialog.yesno("NGL File Server",$tr("Do you want to remove") $$->%item_clicked->$text(0)<br> $tr("with all its subfolders?"))){
 		$$->%rootnode->$removeNode($$->%item_clicked->%virtual_node)
 		$$->%rootnode->$setChanged($true)
		delete $$->%item_clicked
	}
}
else{
	if($dialog.yesno("NGL File Server",$tr("Do you want to remove") $$->%item_clicked->$text(0)?)){
		$$->%item_clicked->$parent()->%virtual_node->$removeFile($$->%item_clicked->$text(0))
		$$->%rootnode->$setChanged($true)
		delete $$->%item_clicked
	}
}
	}
	function returnFromDialog
	{
if (!$0) return
%parent=$$->$getParentNode()
$$->$showNode(%parent)
	}
	internal function rightButtonClickedEvent
	{
if ($$->%nodata) return
	if ($objects.exists($0)) $$->%item_clicked=$0
	else $$->%item_clicked=$$
	%popup=$new(popupmenu)
	$$->%id_add=%popup->$insertItem($tr("Add folder"))
	objects.connect %popup activated $$ popuphandler
 	%popup->$exec()
	delete %popup
	}
	function selectionChangedEvent
	{
if ($objects.exists($0))%item_clicked=$0
	}
	function setChildrenChildren
	{
$0->$setChecked($1)
	%folders[]=$0->$children()
	%len=%folders[]#
	for(%i=0;%i<%len;%i++) $$->$setChildrenChecked(%folders[%i],$1)
	}
	function setChildrenNotChecked
	{
NGLfserv::corefunctions::ngldebug setting true
%exception=$1
%node=$0
%children[]=%node->$children()
NGLfserv::corefunctions::ngldebug numero figli %children[]#
for(%i=0;%i<$length(%children[]);%i++;){
	if (%children[%i]!=%exception) {NGLfserv::corefunctions::ngldebug child %children[%i]->%text non sharato;%children[%i]->%dirNotShared=$true;}
}
	}
	function setParentChecked
	{
%i=$0->$parent
%this=$0
while(%i!=$$){
	if (%i->%virtual_node->$isDirNotChecked()){
		%i->%virtual_node->$setDirNotChecked()
		%i->$setChecked($true)
		$$->$setChildrenNotShared(%i->%virtual_node,%this->%virtual_node)
		%this=%i
		%i=%i->$parent()
	}
	else break
}
	}
	function setWidgetOutput
	{
$$->%info_widget=$0
	}
	function showNode
	{
if (!$objects.exists($0->%virtual_node)) return
%folder[]=$0->%virtual_node->$children()
%virtual_node=$0->%virtual_node
$$->%rootnode->$setNode(%virtual_node)
%listviewitem_node=$0
 // show sub-dir
%length_folder=$length(%folder[])
for (%i=0;%i<%length_folder;%i++){
	%item=$new(listviewitem,%listviewitem_node)
	// %item->$setCheckable($true)
	if ($$->%rootnode->$isDirNotChecked(%folder[%i]) || !$$->$check(%listviewitem_node) || $$->%rootnode->$isDirNotChecked(%folder[%i]->$parent())) %item->$setChecked($false)
	else %item->$setChecked($true)
	%item->$setText(0,$$->%rootnode->$text(%folder[%i]))				
	%item->$setText(1,"Dir")
	if ($$->%rootnode->$isItemHighlighted(%folder[%i]))
		%item->$setPixmap(0,$$->%pix_redfolder)
	else
		%item->$setPixmap(0,$$->%pix_folder)
	%item->%virtual_node=%folder[%i]
	%item_fake_child=$new(listviewitem,%item)
	%item->%fake_child=%item_fake_child
}

// show files
%i=0
	
%len=$$->%rootnode->$filesCount(%virtual_node)
for (%i=0;%i<%len;%i++){
	%item_file=$new(listviewitem,%listviewitem_node)
	// %item_file->$setCheckable($true)
	if ($$->%rootnode->$isFileNotChecked(%virtual_node,%i) || !$$->$check(%listviewitem_node)) %item_file->$setChecked($false)
	else %item_file->$setChecked($true);

	%item_file->$setText(0,$$->%rootnode->$fileAt(%virtual_node,%i))
	%item_file->$setText(1,$NGLfserv::corefunctions::calc($$->%rootnode->$fileSizeAt(%virtual_node,%i)))
				
	switch($str.right($$->%rootnode->$fileAt(%virtual_node,%i),3))
	{
		case (jpg)
		{
			%pix=$$->%pix_image_file
			break
		}
		case (png)
		{
			%pix=$$->%pix_image_file
			break
		}
		case (gif)
		{
			%pix=$$->%pix_image_file
			break
		}
		case (avi)
		{
			%pix=$$->%pix_video_file
			break
		}
		case (mpg)
		{
			%pix=$$->%pix_video_file
			break
		}
		case (mov)
		{
			%pix=$$->%pix_video_file
			break
		}
		case (".rm")
		{
			%pix=$$->%pix_video_file
			break
		}
		case (mp3)
		{
			%pix=$$->%pix_audio_file
			break
		}
		case (".ra")
		{
			%pix=$$->%pix_audio_file
			break
		}
		case (wav)
		{
			%pix=$$->%pix_audio_file
			break
		}
		default
		{
			%pix=$$->%pix_default_file
		}
	}
	%item_file->$setPixmap(0,%pix);
 }
	}
	function showTree
	{
$$->$showNode($$)
	}
}
