alias(NGLfserv::classes::optionsother)
{
	class(NGLfserv::classes::optionsother,widget)
		{
			constructor()
			{
				%colors[]=$array(Dark Blue,Dark Green,Red,\
						Dark Red,Violet,Orange,\
						Yellow,Light Green,Light Blue,Light Grey,\
						Blue,Light Violet,Dark Grey,Grey)
	
				%main_layout=$new(layout,$this)
				%main=$new(NGLfserv::classes::customwidgets::box,$$,box,vertical)
				%vbox=%main->$addWidget(vbox)
				%vbox->$setSpacing(2)
				%hbox1 = $new(hbox,%vbox)
				%hbox1->$setSpacing(2)
				%hbox2 = $new(hbox,%vbox)
				%hbox2->$setSpacing(2)
				%vbox->$setStretchFactor(%hbox1,90)
	
				 // Box color properties
				%gbColors=$new(NGLfserv::classes::customwidgets::groupbox,%hbox1,gbColors,"File server colors",5,20)
				%gbColors->$setColumnLayout(1,Vertical)
				%gbColors->$setTooltip($tr("Set the colors of File Server messages"))
				%box_colors_labels=$new(NGLfserv::classes::customwidgets::box,%gbColors,box,vertical)
				%box_colors_labels->$setSpacing(1)
				%box_colors_labels->$addGroup("label",$array("Primary Color:","Secondary Color:"),120)
				%box_colors_params=$new(NGLfserv::classes::customwidgets::box,%gbColors,box,vertical)
				%box_colors_params->$setSpacing(1)
				$$->%comboBoxColor1=%box_colors_params->$addWidget(combobox,%colors[],100)
				$$->%comboBoxColor2=%box_colors_params->$addWidget(combobox,%colors[],100)
				$$->%comboBoxColor1->$setCurrentItem($(%NGLc1 - 2))
				$$->%comboBoxColor2->$setCurrentItem($(%NGLc2 - 2))
	
				// Box find properties
				%gbFind=$new(NGLfserv::classes::customwidgets::groupbox,%hbox1,gbFind,"@find properties",5,20)
				%gbFind->$setColumnLayout(1,Vertical)
				%temp_hbox=$new(hbox,%gbFind)
				%temp_hbox->$setMaximumWidth(150)
				%box_find_labels=$new(NGLfserv::classes::customwidgets::box,%temp_hbox,box,vertical)
				%box_find_labels->$addGroup(label,$array("Max results:","Max cache:"),100)
				%box_find_params=$new(NGLfserv::classes::customwidgets::box,%temp_hbox,box,vertical)
				%findMsgs = %box_find_params->$addWidget(spinbox,"\%FindMsg",60,100)
				%maxCache = %box_find_params->$addWidget(spinbox,"\%MaxCacheFind",60,1000)
				%findMsgs->$setTooltip($tr("Maximum number of reported found results"))
				%maxCache->$setTooltip($tr("<b>(Advanced)</b> Maxium number of cached results"))
				%box_find_checkboxes=$new(NGLfserv::classes::customwidgets::box,%gbFind,box,vertical)
				%ckbDisplayNoOccurrenceFound=%box_find_checkboxes->$addWidget(checkbox,"Display \"No files found\"","\%DisplayNoOccurrenceFound")
				%ckbDisplayNoOccurrenceFound->$setTooltip($tr("<b>(Discouraged)</b> Choose if you want to inform that no matching files were found"))
				%quickget = %box_find_checkboxes->$addWidget(checkbox,"Enable quickget","\%B_quickget")
				%filtering = %box_find_checkboxes->$addWidget(checkbox,"Enable filtering","\%FindFilter")
				%multiple = %box_find_checkboxes->$addWidget(checkbox,"Mutiple results per line","\%MultipleResultsPerLine")
				%quickget->$setTooltip($tr("Enable getting files without passing from navigation shell"))
				%filtering->$setTooltip($tr("Does not report found results to users that are not allowed to access the trigger"))
				%multiple->$setTooltip($tr("Outputs multiple results per line reducing lag effects in case of many results"))
				%hbox1->$setStretchFactor(%gbFind,60)
	
				// Box debug
				%gbDebug=$new(NGLfserv::classes::customwidgets::groupbox,%hbox2,gbDebug,"Debug",5,20)
				%gbDebug->$setColumnLayout(1,Vertical)
				%debug_box=$new(NGLfserv::classes::customwidgets::box,%gbDebug,box,vertical)
				%debug_box->$setSpacing(1)
				%debug_box->$addWidget(checkbox,"Enable Debugging","\%G_NGLdebug")
	
				%main_layout->$addWidget(%main->$widget,0,0)
	
				privateimpl($$->%comboBoxColor1,activatedEvent) {
					%NGLc1=$($$->$currentItem() + 2)
				}
				privateimpl($$->%comboBoxColor2,activatedEvent) {
					%NGLc2=$($$->$currentItem() + 2)
				}
			}
		}
}


