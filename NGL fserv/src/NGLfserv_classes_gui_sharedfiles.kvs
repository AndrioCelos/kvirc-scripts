alias(NGLfserv::classes::gui::sharedfiles)
{
	class(NGLfserv::classes::gui::sharedfiles,widget)
				{
					constructor()
					{
						$$->$setCaption(".:Shared Files:.")
						%main_layout=$new(layout,$this)
						%main = $new(NGLfserv::classes::customwidgets::box,$$,box,vertical)
	
						// Trigger combobox
						%hbox=%main->$addWidget(hbox)
						%hbox->$setSpacing(2)
						%gb=$new(NGLfserv::classes::customwidgets::groupbox,%hbox,gb,"Trigger",5,20)
						%gb->$setColumnLayout(1,Horizontal)
						$$->%comboboxTriggers=$new(combobox,%gb)
	
						// Operations buttons
						%gb=$new(NGLfserv::classes::customwidgets::groupbox,%hbox,gb,"Operations",5,20)
						%gb->$setColumnLayout(1,Horizontal)
						$$->%box_buttons=$new(NGLfserv::classes::customwidgets::box,%gb,box,horizontal)
						$$->%box_buttons->$setSpacing(1)
						%add=$$->%box_buttons->$addWidget(button,"Add")
						%add->$setImage(redfolder)
						%rem=$$->%box_buttons->$addWidget(button,"Remove")
						%rem->$setImage(redfoldercancel)
						$$->%check=$$->%box_buttons->$addWidget(button,"Update")
						$$->%check->$setImage(ok)
						//%check->$setEnabled($false)
	
						// Dirview
						%hbox=%main->$addWidget(hbox)
						%hbox->$setSpacing(2)
						$$->%gbFiles=$new(NGLfserv::classes::customwidgets::groupbox,%hbox,gb,"Trigger files",5,20)
						$$->%gbFiles->$setColumnLayout(1,Vertical)
						%box=$new(NGLfserv::classes::customwidgets::box,$$->%gbFiles,box,vertical)
						/*$$->%status=%box->$addWidget(label,"Status: \<font color=\"#808080\"> Ready")
						$$->%status->$setminimumwidth(100)
						$$->%status->$setmaximumwidth(100)
						*/
						$$->%dirview=%box->$addWidget(NGLfserv::classes::dirview)
	
						// Output
						$$->%info_widget=%box->$addWidget(multilineedit)
						//$$->%status=%box->$addWidget(label,"Status: \<font color=\"#808080\"> HD scanning")
						//"Status: Ready"
						//$setText)
						//%box->$setStretchFactor($$->%info_widget,10)
						//%box->$setStretchFactor($$->%dirview,90)
						$$->%dirview->$setMinimumheight(250)
						//$$->%dirView->$setWidgetOutput($$->%info_widget)
						//$$->%dirView->$setStatusOutput(%status)
						%main_layout->$addWidget(%main->$widget(),0,0)
	
						$$->%comboboxTriggers->$setTooltip($tr("Browse your shared files by trigger"))
						%add->$setTooltip($tr("Add other files to this trigger"))
						%rem->$setTooltip($tr("Remove the files you don't want to share from this trigger"))
						$$->%check->$setTooltip($tr("If some file have been phisically added/removed to the trigger folder while KVIrc is opened, click here to update your shared files list"))
	
						objects.connect $$->%comboboxTriggers activated $$ newTrigger
						objects.connect %add clicked $$ addFolder
						objects.connect %rem clicked $$ removeFolder
						objects.connect $$->%check clicked $$ updateFolders
					}
					addFolder()
					{
						if (!$$->%dirview->%item_clicked) {echo vuoto;$$->%dirview->%item_clicked=$$->%dirview;}
						$$->%dirview->$addTo($$->%dirview->%item_clicked)
					}
					removeFolder()
					{
						if (!$$->%dirview->%item_clicked) NGLfserv::corefunctions::nglwarning "You must choose a file or folder first!"
						else{
							if ($$->%dirview->%item_clicked!=$$->%dirview)
							     $$->%dirview->$remove($$->%dirview->%item_clicked)
						}
					}
					updateFolders()
					{
						//$$->%status->$setText("Status: \<font color=\"#808080\"> HD scanning")
						$$->%check->$setEnabled($false)
						$$->%dirview->$setEnabled($false)
						$$->%info_widget->$append("Updating shared dirs/files...")
						$$->%dirview->%trigger->$update()
					}
					newTrigger()
					{
						%triggername=$$->%comboboxTriggers->$textAt($0)
						if (%triggername)
						{
							$$->%gbFiles->$setTitle(%triggername" files")
							%trigger=%Triggers{%triggername}
							$$->%dirview->$clear()
							$$->%dirview->$setTree(%trigger)
							objects.connect %trigger scancompleted $$ status
							$$->%dirview->$showTree()
						}
					}
					status()
					{
						$$->%info_widget->$append("Updating completed!")
						$$->%check->$setEnabled($true)
						$$->%dirview->$setEnabled($true)
					}
					showEvent()
					{
						$$->%comboboxTriggers->$clear()
						$$->%comboboxTriggers->$setEnabled($false)
						$$->%box_buttons->$setEnabled($false)
						$$->%dirView->$setEnabled($false)
						if ($objects.exists($$->%comboboxTriggers))
						{
							if ($length(%Triggers{}))
							{
								$$->%comboboxTriggers->$setEnabled($true)
								$$->%box_buttons->$setEnabled($true)
								$$->%dirView->$setEnabled($true)
								foreach(%tr,%Triggers{}) $$->%comboboxTriggers->$insertItem(%tr->$triggerName())
								$$->%comboboxTriggers->$setEnabled($true)
								$$->$newTrigger($$->%comboboxTriggers->$currentItem)
							}
						}
					}
				}
}


