alias(NGLfserv::classes::gui::wiz)
{
	class(NGLfserv::classes::gui::wiz,wizard)
	{
		constructor()
		{
			$$->$setWelcomePage()
			$$->$setTriggerPage()
			$$->$setChannelPage()
			$$->$setFindPage()
			$$->$setReportPage()
			$$->$setNextEnabled($$->%pages{trigger_page},$false)
			$$->$setNextEnabled($$->%pages{channel_page},$false)
			$$->$setWFlags(TopLevel)
			$$->$setCaption(%NGLlogo" - Wizard")
			$$->$setIcon(110)
			$$->$show()
		}
		setWelcomePage()
		{
			%insidepage=$$->$setBaseWidget("welcome_page","Welcome to the NGLfserver configuration wizard")
			
			%label=$new(label,%insidepage)
			%label->$setImage($file.localdir(pics/NGLfserv/NGLlogo.png))
			%label->$setAlignment("Hcenter")
				
			%label=$NGLfserv::corefunctions::addWidget(label,%insidepage,"<p align="justify">This procedure will help you in the creation of a trigger. You will have to insert the trigger name, the shared directory and the channel where the trigger works.</p><p align="justify">You can also setup the main options or leave them just as suggested by the wizard. Further on you could personalize them using the preferences menu.</p><p><b><u>Click Next to start.</b></u></p>")
			
			%insidepage->$setStretchfactor(%label,200)		
		}
		setBaseWidget()
		{
			%page=$new(widget,$$)
			%hbox=$new(hbox,%page)
			%hbox->$setSpacing(2)
			%label=$new(label,%hbox)
			%label->$setImage($file.localdir(pics/NGLfserv/NGLwizardBackgroundPixmap.png))
			%label->$setAlignment("Left","Bottom")
			%hbox->$setStretchfactor(%label,20)
			$$->$addPage(%page,$1)
			$$->%pages{$0}=%page
			$$->$setHelpEnabled(%page,$tr($2))
			%layout=$new(layout,%page)
			%layout->$addWidget(%hbox,0,0)
			%vbox=$new(vbox,%hbox)
			%hbox->$setStretchfactor(%vbox,200)	
			return %vbox	
		}
		setTriggerPage()
		{
			%insidepage=$$->$setBaseWidget("trigger_page","Choose trigger and shared directory",$false)
					
			%gbox=$NGLfserv::corefunctions::addWidget(groupbox,%insidepage,"Choose Trigger",,,Horizontal,1)
			%insidepage->$setStretchFactor(%gbox,20)
			$$->%labelTrigger=$NGLfserv::corefunctions::addWidget(label,%gbox,"<p>Specify a word to identify the shared files.</p>")
			
			$$->%triggerName=$new(lineedit,%gbox)
			objects.connect $$->%triggerName textChanged $$ checktriggerparameters
			
			%gbox=$NGLfserv::corefunctions::addWidget(groupbox,%insidepage,"Choose folder",,,Horizontal,1)
			%insidepage->$setStretchFactor(%gbox,80)
			$$->%labelFolder=$NGLfserv::corefunctions::addWidget(label,%gbox,"<p align="justify">Choose the shared directory. Further on you could add more folders from the preferences, hanging them up virtually to this one</p>")
			
			%hbox2=$new(hbox,%gbox)
			%hbox2->$setSpacing(2)
			
			$$->%folderName=$new(lineedit,%hbox2)
			objects.connect $$->%folderName textChanged $$ checktriggerparameters
			
			%btn=$NGLfserv::corefunctions::addWidget(button,%hbox2,"Browse")
			
			objects.connect %btn clicked $$ addFolder
		}
		addFolder()
		{
			dialog.file(dir,%NGLlogo,,,$$->%folderName)
			{
				if ($0) $1->$setText($0)
			}
		}
		checkTriggerParameters()
		{
			%bool1 = $true
			%bool2 = $true
			if ($$->%triggerName->$text()){
				if (%Triggers{$$->%triggerName->$text()}) {
					%bool1 = $false
					$$->%labelTrigger->$setText("<p>Specify a word to identify the shared files.</p><p><b><font color="red">TRIGGER NAME ALREADY PRESENT</font></b></p>")
				} else if($str.contains($$->%triggerName->$text(),"[") || $str.contains($$->%triggerName->$text(),"]") || $str.contains($$->%triggerName->$text()," ")) {
					$$->%labelTrigger->$setText("<p>Specify a word to identify the shared files.</p><p><b><font color="red">WHITESPACES AND [ ] CHARS ARE NOT ALLOWED</font></b></p>")
					%bool1 = $false
				} else {
					$$->%labelTrigger->$setText("<p>Specify a word to identify the shared files.</p>")
					%bool1 = $true
				}
			} else {
				$$->%labelTrigger->$setText("<p>Specify a word to identify the shared files.</p>")
				%bool1 = $false
			}
			if($$->%folderName->$text()) {
				if (!$file.exists($$->%folderName->$text())) {
					%bool2 = $false
					$$->%labelFolder->$setText("<p align="justify">Choose the shared directory. Further on you could add more folders from the preferences, hanging them up virtually to this one</p><p><b><font color="red">INVALID FOLDER</font></b></p>")
				} else {
					%bool2 = $true
					$$->%labelFolder->$setText("<p align="justify">Choose the shared directory. Further on you could add more folders from the preferences, hanging them up virtually to this one</p>")
				}
			} else {
				$$->%labelFolder->$setText("<p align="justify">Choose the shared directory. Further on you could add more folders from the preferences, hanging them up virtually to this one</p>")
				%bool2 = $false
			}
			$$->$setNextEnabled($$->%pages{trigger_page},$(%bool1 && %bool2))
		}
		setChannelPage()
		{
			%insidepage=$$->$setBaseWidget("channel_page","Choose the channel in which the trigger works",$false)
			
			// channel 
			%gbox=$NGLfserv::corefunctions::addWidget(groupbox,%insidepage,"Choose Channel",,,Horizontal,1)
			%insidepage->$setStretchFactor(%gbox,20)
			%label=$NGLfserv::corefunctions::addWidget(label,%gbox,"<p>Now add the channel name where you want to share your files, <b>prepended by #, & or others suffixes if any.</b></p>")
			$$->%channelName=$new(lineedit,%gbox)
			objects.connect $$->%channelName textChanged $$ checkchannelparameters
			%gbox=$NGLfserv::corefunctions::addWidget(groupbox,%insidepage,"Choose Channel's Options",,,Horizontal,1)
			%insidepage->$setStretchFactor(%gbox,70)
			
			// channel options
			%label=$NGLfserv::corefunctions::addWidget(label,%gbox,"<p>Choose the options for the channel (if you don't know, leave them as they are).</p>")
			$$->%checklist=$NGLfserv::corefunctions::addWidget(checkbox,%gbox,"<p>This option let the users know if your trigger is active on this channel</p>",$true)
			$$->%checkmyqueues=$NGLfserv::corefunctions::addWidget(checkbox,%gbox,"<p>This option let the users know the status of their queued files</p>",$false)
			%vbox=$new(vbox,%gbox)
			%label=$NGLfserv::corefunctions::addWidget(label,%vbox,"Here you can choose the access type for the trigger:")
			%boxh=$new(hbox,%vbox)
			$$->%triggertype=$new(listbox,%boxh)
			$$->%triggertype->$insertItem("Both")
			$$->%triggertype->$insertItem("Remote")
			$$->%triggertype->$insertItem("Ctcp")
			$$->%triggertype->$setMaximumWidth(60)
			$$->%triggertype->$setMaximumHeight(60)
			$$->%triggertype->$setCurrentItem(0)
			%label=$NGLfserv::corefunctions::addWidget(label,%boxh,"<ul><li>ctcp: the access will be such as /ctcp yournick trigger</li><li>remote: the user will digit the trigger name in the channel instead</li></ul>")
		}
		checkChannelParameters()
		{
			if ($str.strip($$->%channelName->$text())) $$->$setNextEnabled($$->%pages{channel_page},$true)
			else $$->$setNextEnabled($$->%pages{channel_page},$false)
		}
		setFindPage()
		{
			%insidepage=$$->$setBaseWidget("find_page","Choose @find options",$false)
			%gbox=$NGLfserv::corefunctions::addWidget(groupbox,%insidepage,"Choose @find options",,,Horizontal,1)
			%insidepage->$setStretchfactor(%gbox,200)
					
			%label=$NGLfserv::corefunctions::addWidget(label,%gbox,"<p>Choose @find special options (if you don't know, leave them as they are).</p>")
			
			$$->%checkqget=$NGLfserv::corefunctions::addWidget(checkbox,%gbox,"<p>This option activates the quickget that permits to download a file without passing by the navigation window.</p>",$true)
			$$->%checkmres=$NGLfserv::corefunctions::addWidget(checkbox,%gbox,"<p>This option let the @find show as many results as possible per line, reducing lag in case of many occurences.</p>",$true)
			$$->%checknooccurrence=$NGLfserv::corefunctions::addWidget(checkbox,%gbox,"<p>Checking this the file server will notify the user in case the @find didn't find anything.</p>",$false)
		
		}
		setReportPage()
		{
			%insidepage=$$->$setBaseWidget("report_page","Summary",$false)
		
			%label=$NGLfserv::corefunctions::addWidget(label,%insidepage,"")	
			%label->$setAlignment("Left","Top")
			%label->%wiz=$$
			privateimpl(%label,showEvent)
			{
				// hack to fix KVIrc bug ?!?
				if (!$objects.exists($$->%wiz) || !$objects.exists($$->%wiz->%triggerName)) return 
				// 
				
				%szText<<"<b>Trigger name:</b> "<ul><li>$$->%wiz->%triggerName->$text()"</li></ul>"
				%szText<<"<b>Channel name:</b> "<ul><li>$$->%wiz->%channelName->$text()"</li></ul>"
				
				if ($$->%wiz->%checkqget->$isChecked()) %szOpts="<ul><li>Quickget </li>"
				if ($$->%wiz->%checkmres->$isChecked()) %szOpts<<"<li>Multiline @find output </li>"
				if ($$->%wiz->%checknooccurrence->$isChecked()) %szOpts<<"<li>Display \"No occurrences found\" </li>"
				if ($$->%wiz->%checklist->$isChecked()) %szOpts<<"<li> Reply to !list </li>"
				if ($$->%wiz->%checkmyqueues->$isChecked()) %szOpts<<"<li> Reply to !myqueues </li>"
				switch($$->%wiz->%triggertype->$currentText()){
					case ("Ctcp"){
						%szOpts<<"<li>Ctcp access</li>"
						break
					}
					case ("Remote"){
						%szOpts<<"<li>Remote access</li>"
						break
					}
					default:{
						%szOpts<<"<li>Ctcp and remote access</li>"
						break
					}
				}
				if (%szOpts) %szText<<"<b>Main active options:</b>"%szOpts"</ul>"
				$$->$setText(%szText)
			}
					
			%label=$NGLfserv::corefunctions::addWidget(label,%insidepage,"<b>Folder to share:</b>")	
			
			%label=$new(label,%insidepage)
			%label->%wiz=$$
			privateimpl(%label,showEvent)
			{
				// hack to fix KVIrc bug ?!?
				if (!$objects.exists($$->%wiz->%folderName)) return
				//	
				$$->$setText("<ul><li>"$$->%wiz->%folderName->$text()"</li></ul>")
			}
	
			%label1=$NGLfserv::corefunctions::addWidget(label,%insidepage,"<br><p><b><u>Click Finish to complete the trigger creation.</u></b></p>")
			
			$$->$setFinishEnabled($$->%pages{report_page},$true)
		}
		internal function acceptEvent()
		{
			// creating trigger
			%path=$$->%folderName->$text()
			if ($str.right(%path,1)!="\/") %path .= \/
			%trigger=$new(NGLfserv::classes::trigger,0)
			%trigger->$addFirstNode(%path,$$->%triggerName->$text())
			%Triggers{$$->%triggerName->$text()}=%trigger
			%trigger->$setChanged($true)
			
			// then add channel
			NGLfserv::corefunctions::channels a $$->%channelName->$text() $$->%triggerName->$text()
				
			// if the gui exists then add triggername and channelname to their rispective listbox
			if ($objects.exists(%NGLgui)){
				%NGLgui->%tabTriggers->%listboxTriggers->$insertItem($$->%triggerName->$text())
				%NGLgui->%tabTriggers->%listboxChannels->$insertItem($$->%channelName->$text())
			}
			
			// setting trigger option
			%trigger->$getChannel($$->%channelName->$text())->$setTriggerType($$->%triggertype->$currentItem())
			
			// now setting channel options
			%FservChannels{$$->%channelName->$text()}->$setRespToMyQueues($$->%checkmyqueues->$isChecked())
			%FservChannels{$$->%channelName->$text()}->$setUseQuickGet($$->%checkqget->$isChecked())
			%FservChannels{$$->%channelName->$text()}->$setMultipleResultsPerLine($$->%checkmres->$isChecked())
			%FservChannels{$$->%channelName->$text()}->$setDisplayNoOccurrenceFound($$->%checknooccurrence->$isChecked())		
			
			NGLfserv::corefunctions::nglok "<p>The trigger is been added succesfully: it will be operating as the file scan finishes.</p>"
			NGLfserv::config::save
			delete $this		
		}
		internal rejectEvent()
		{
			delete $this
		}
	}
}

