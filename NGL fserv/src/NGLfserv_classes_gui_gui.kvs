alias(NGLfserv::classes::gui::gui)
{
	class (NGLfserv::classes::gui,tabwidget)
	{
		constructor()
		{
			$$->%tabTriggers=$new(NGLfserv::classes::gui::triggers,$$)
			$$->%tabChannels=$new(NGLfserv::classes::gui::channels,$$)
			$$->%tabSharedFiles=$new(NGLfserv::classes::gui::sharedfiles,$$)
			$$->%tabShell=$new(NGLfserv::classes::gui::shell,$$)
			$$->%tabGlobals=$new(NGLfserv::classes::gui::globals,$$)
	
			$$->$setMargin(5)
			$$->$setTabPosition(Top)
	
			$$->$addTab($$->%tabTriggers,&Triggers,42)
			$$->$setTabToolTip($$->%tabTriggers,$tr("Filling up this section is required to use the File Server. You're asked to do three steps:<br><ul><li>Specify a trigger (a keyword) to identify the files you want to share.</li><li>For each trigger type the channels in witch it will be active.</li><li><b>(Optional)</b> For each channel specify trigger properties</li></ul>"))
	
			$$->$addTab($$->%tabChannels,&Channels,31)
			$$->$setTabToolTip($$->%tabChannels,$tr("Specify properties for channels on witch you share you files"))
	
			$$->$addTab($$->%tabSharedFiles,&Shared Files,161)
			$$->%busywidget=$$->%tabSharedFiles
			$$->$setTabToolTip($$->%tabSharedFiles,$tr("Manage your shared files"))
			
			$$->$addTab($$->%tabShell,"S&hell",9)
			$$->$setTabToolTip($$->%tabShell,$tr("Specify options regarding navigation shell"))
	
			$$->$addTab($$->%tabGlobals,"&Globals",260)
			$$->$setTabToolTip($$->%tabGlobals,$tr("Specify options that applies globally to the File Server"))
		
			%size[]=$$->$sizeHint()
			$$->$setMinimumWidth(%size[0])
			$$->$setMinimumHeight(%size[1])
			$$->$setCaption(%NGLlogo" - Options")
			$$->$setIcon(20)
			$$->$centerToScreen()
			$$->$show()
			$$->$raise()
		}
		internal closeEvent()
		{
			NGLfserv::config::save
		}
		internal showEvent()
		{
			$$->$centerToScreen()
		}
	}
}

