alias(NGLfserv::classes::optionschannels)
{
	class(NGLfserv::classes::optionschannels,widget)
				{
				constructor()
				{
					%lay=$new(layout,$$)
	
					%main=$new(NGLfserv::classes::customwidgets::box,$$,box,vertical)
					%hbox=%main->$addWidget(hbox)
					%hbox->$setSpacing(2)
	
					// channels list
					%gb=$new(NGLfserv::classes::customwidgets::groupbox,%hbox,gb,"Channels list",5,20)
			 		%gb->$setColumnLayout(1,Vertical)
			 		$$->%listboxChannels=$new(listbox,%gb)
	
			  		// channel properties
					$$->%gb_channelproperties=$new(NGLfserv::classes::customwidgets::groupbox,%hbox,gb,"Properties",5,20)
			 		$$->%gb_channelproperties->$setColumnLayout(1,Vertical)
					%box_properties=$new(NGLfserv::classes::customwidgets::box,$$->%gb_channelproperties,box,vertical)
					%box_properties->$setSpacing(1)
					$$->%ckbRespToList=%box_properties->$addWidget("checkbox","Respond to !list")
					$$->%ckbRespToFind=%box_properties->$addWidget("checkbox","Respond to @find")
					$$->%ckbRespToMyQueues=%box_properties->$addWidget("checkbox","Respond to !myqueues")
					%lay->$addWidget(%main->$widget,0,0)
	
					$$->%ckbRespToList->$setTooltip($tr("The File Server will respond when !list is typed in channel, providing the list of your shared files"))
					$$->%ckbRespToFind->$setTooltip($tr("The File Server will respond when @find filename is typed in channel and \"filename\" is found in your shared files"))
					$$->%ckbRespToMyQueues->$setTooltip($tr("The File Server will respond when !myqueues is typed in channel providing to the user the list of his queued files"))
	
					objects.connect $$->%listboxChannels currentItemChange $$ selectionChange
					objects.connect $$->%ckbRespToList toggled $$ checkHandler
					objects.connect $$->%ckbRespToFind toggled $$ checkHandler
					objects.connect $$->%ckbRespToMyQueues toggled $$ checkHandler
				}
				checkHandler()
				{
					/* gestione "finti" eventi checkbox che vengono triggerati quando si passa da una tab a questa
					   L'evento viene triggerato quando la cb passa dallo stato di true a false
					   Noi non facciamo altro che contare quante cb sono variate e scartarle qui nell'evento
					*/
					if ($$->%flagcb){
						if ($$->%flagcount<$$->%nrfakecheckbox){
							$$->%flagcount++
							return
						}
						else{
							$$->%flagcount=0
							$$->%flagcb=0
						}
					}
					%src=$$->$signalSender()
					%channelName=$$->%listboxChannels->$currentText()
					if (!%channelName) return
					if (%src==$$->%ckbRespToList) %FservChannels{%channelName}->$setRespToList($0)
					else if (%src==$$->%ckbRespToFind) %FservChannels{%channelName}->$setRespToFind($0)
					else if (%src==$$->%ckbRespToMyQueues) %FservChannels{%channelName}->$setRespToMyQueues($0)
	
				}
	
				selectionChange()
				 {
					%channelName=$$->%listboxChannels->$currentText()
					if(%channelName){
						$$->%gb_channelproperties->$setEnabled($true)
						$$->%gb_channelproperties->$setTitle(%channelName" properties")
						$$->%ckbRespToList->$setChecked(%FservChannels{%channelName}->$getRespToList())
						$$->%ckbRespToFind->$setChecked(%FservChannels{%channelName}->$getRespToFind())
						$$->%ckbRespToMyQueues->$setChecked(%FservChannels{%channelName}->$getRespToMyQueues())
					}
				}
				showEvent()
				{
					if (!$objects.exists($$->%listboxChannels)) return
					// Inizio trick x evitare l'azzeramento delle checkbox quando si torna a questa tab
					// Qui contiamo quante cb  triggerano l'evento del toggle x poi scartare i relativi eventi
					$$->%flagcount=0
					$$->%nrfakecheckbox=0
					$$->%gb_channelproperties->$setEnabled($false)
					if ($$->%ckbRespToList->$isChecked) $$->%nrfakecheckbox++
					if ($$->%ckbRespToFind->$isChecked) $$->%nrfakecheckbox++
					if ($$->%ckbRespToMyQueues->$isChecked) $$->%nrfakecheckbox++
					// Se almeno una delle cb Ã¨ true allora attiviamo il flag x avvertire l'evento
					if ($$->%nrfakecheckbox) $$->%flagcb=$true
					// fine trick
	
					$$->%ckbRespToList->$setChecked($false)
					$$->%ckbRespToFind->$setChecked($false)
					$$->%ckbRespToMyQueues->$setChecked($false)
						$$->%listboxChannels->$clear()
					if($length(%FservChannels{})){
						foreach(%ch,%FservChannels{}){
								if ($objects.exists(%ch))
								 	$$->%listboxChannels->$insertItem(%ch->$getChannelName())
						}
					}
					else $$->%gb_channelproperties->$setTitle("Properties")
	
				}
			}
}


